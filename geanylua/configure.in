
AC_INIT([geanylua],0.7.0, [yetanothergeek@gmail.com])

PACKAGE="geanylua"
VERSION=0.7.0
GEANY_MIN_VER=0.13

AC_SUBST([VERSION])
AC_SUBST([PACKAGE])

AM_INIT_AUTOMAKE([${PACKAGE}], ${VERSION} )


AC_PROG_CC
AC_LANG_C
AC_PROG_MAKE_SET
AC_HEADER_STDC

AC_DISABLE_STATIC
AM_PROG_LIBTOOL


PKG_CHECK_MODULES(GEANY, [geany])
AC_SUBST(GEANY_CFLAGS)
AC_SUBST(GEANY_LIBS)


AC_ARG_WITH([lua-pkg],
 AC_HELP_STRING([--with-lua-pkg=ARG],[name of Lua pkg-config script [[default=lua5.1]]]),
 [LUA_PKG_NAME=${withval%.pc}],
[
LUA_PKG_NAME=lua5.1

for L in lua5.1 lua51 lua-5.1 lua
do
  AC_MSG_CHECKING([for $L.pc])
  if ${PKG_CONFIG} --exists ${L}
  then
    AC_MSG_RESULT([yes])
    LUA_PKG_NAME=${L}
    break
  else
    AC_MSG_RESULT([no])
  fi
done
]
)


PKG_CHECK_MODULES([LUA], [${LUA_PKG_NAME} >= 5.1])
AC_SUBST(LUA_CFLAGS)
AC_SUBST(LUA_LIBS)


AC_ARG_ENABLE([hidden-symbols],
 AC_HELP_STRING([--enable-hidden-symbols],[Hide internal symbols in library [[default=no]]]),
 [
  if ${CC} --help --verbose 2> /dev/null | grep "\-fvisibility\>" > /dev/null
  then
    AC_DEFINE([PLUGIN_EXPORT], [__attribute__ ((visibility ("default")))], [to make a symbol visible])
    OS_FLAGS="${OS_FLAGS} -fvisibility=hidden"
    HIDE_SYMS="yes"
  else
    AC_MSG_ERROR([--enable-hidden-symbols option is not supported by compiler!])
    HIDE_SYMS="no"
  fi
 ],
 [
  OS_FLAGS=
 ]
)


AC_ARG_ENABLE([sys-install],
AC_HELP_STRING([--enable-sys-install],[install plugin for all users [[default=no]]]),
 [
	if test "x${prefix}" = "xNONE"; then
   prefix=${ac_default_prefix}
	fi
  SYS_INSTALL=yes
  PLUGIN_DEST_DIR="${prefix}/lib/geany"
  DATA_DEST_DIR="${prefix}/share/geany/plugins/${PACKAGE}"
  DOC_DEST_DIR="${prefix}/share/doc/geany-plugins/${PACKAGE}"
 ],
 [
  SYS_INSTALL=no
  PLUGIN_DEST_DIR="${HOME}/.geany/plugins"
  DATA_DEST_DIR="${HOME}/.geany/plugins/${PACKAGE}"
  DOC_DEST_DIR="${HOME}/.geany/plugins/${PACKAGE}/support"
 ]
)

AC_SUBST(SYS_INSTALL)
AC_SUBST(PLUGIN_DEST_DIR)
AC_SUBST(DATA_DEST_DIR)
AC_SUBST(DOC_DEST_DIR)

case "${host_os}" in
  cygwin* | mingw*)
    if test "x${SYS_INSTALL}" = "xyes"
    then
      AC_MSG_ERROR([--enable-sys-install option is not supported on windows!])
    fi
    if test "x${HIDE_SYMS}" = "xyes"
    then
      AC_MSG_ERROR([--enable-hidden-symbols option is not supported on windows!])
    fi
    LIBEXT="dll"
    OS_FLAGS="${OS_FLAGS} -mms-bitfields"
    USER_CMD="'make win32'"
    AC_DEFINE([PLUGIN_EXPORT], [__declspec(dllexport)], [to make a symbol visible])
  ;;
  *)
    LIBEXT="so"
    USER_CMD="'make'"
  ;;
esac

LIBRARY="libgeanylua"

PLUGIN_FILENAME="${PACKAGE}.${LIBEXT}"
LIBRARY_FILENAME="${LIBRARY}.${LIBEXT}"

AC_SUBST(OS_FLAGS)
AC_SUBST(PLUGIN_FILENAME)
AC_SUBST(LIBRARY)
AC_SUBST(LIBRARY_FILENAME)


AC_CONFIG_FILES([Makefile])
AC_OUTPUT

echo
echo "========================================"
make pathcheck

echo
echo "Plugin filename:        ${PLUGIN_FILENAME}"
echo "System-wide install:    ${SYS_INSTALL}"
echo
echo "Configuration complete, now type ${USER_CMD}"
echo


