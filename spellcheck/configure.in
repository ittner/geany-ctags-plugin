# Process this file with autoconf to produce a configure script.
# $Id: configure.in 2 2008-04-02 14:52:33Z eht16 $

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(spellcheck, 0.2)

AM_CONFIG_HEADER(config.h)

AC_PROG_CC

AC_DISABLE_STATIC
AM_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --silent"

AC_PROG_INSTALL

# checking for Geany
PKG_CHECK_MODULES(GEANY, [geany])
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)

GEANY_VERSION=`$PKG_CONFIG --modversion geany`
GTK_VERSION=`$PKG_CONFIG --modversion gtk+-2.0`

# checking for aspell
AC_CHECK_HEADER(aspell.h, [AC_DEFINE(HAVE_ASPELL_H, 1,
   [Define to 1 if you have the <aspell.h> header file.] )],
      [AC_MSG_ERROR([You must have the aspell dev library installed to build the spellcheck plugin.]) ] )
AC_CHECK_LIB(aspell, new_aspell_speller, SPELLER_LIB="-laspell",
      [AC_MSG_ERROR([You must have the aspell dev library installed to build the spellcheck plugin.]) ] )
AC_SUBST(SPELLER_LIB)

# i18n
GETTEXT_PACKAGE=spellcheck
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])

ALL_LINGUAS="`sed -e '/^#/d' $srcdir/po/LINGUAS`" # take all languages found in file po/LINGUAS

AM_GLIB_GNU_GETTEXT
# workaround for intltool bug (http://bugzilla.gnome.org/show_bug.cgi?id=490845)
if test "x$MSGFMT" = "xno"; then
	AC_MSG_ERROR([msgfmt not found. Please install the gettext package.])
fi

# intltool hack to define install_sh on Debian/Ubuntu systems
if test "x$install_sh" = "x"; then
	install_sh="`pwd`/install-sh"
	AC_SUBST(install_sh)
fi

# get the plugin installed at the correct location for Geany
# TODO find a way to NOT override --libdir command line option if given
libdir="`$PKG_CONFIG --variable=libdir geany`/geany"


AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
])

echo "----------------------------------------"
echo "Install Spell Check plugin binary in : ${libdir}"
echo "Install Spell Check files in         : ${prefix}"
echo "Using Geany version                  : ${GEANY_VERSION}"
echo "Using GTK version                    : ${GTK_VERSION}"
echo ""
echo "Configuration is done OK."
echo ""
