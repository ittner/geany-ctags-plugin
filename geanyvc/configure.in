dnl Process this file with autoconf to produce a configure script.

AC_INIT([geanyvc], [0.5], [yurand2@gmail.com])
AM_INIT_AUTOMAKE([1.9 foreign])

AM_CONFIG_HEADER(config.h)

AC_PROG_CC

AC_DISABLE_STATIC
AM_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --silent"

AC_PROG_INSTALL
AC_PROG_INTLTOOL


# checking for Geany
PKG_CHECK_MODULES(GEANY, [geany >= 0.16])
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)

GEANY_VERSION=`$PKG_CONFIG --modversion geany`
GTK_VERSION=`$PKG_CONFIG --modversion gtk+-2.0`


dnl Check for GtkSpell support
AC_MSG_CHECKING([whether to use GtkSpell])
AC_ARG_ENABLE(gtkspell,
        [  --disable-gtkspell      Do not use GtkSpell in editor],
        [ac_cv_enable_gtkspell=$enableval], [ac_cv_enable_gtkspell=yes])
if test "$ac_cv_enable_gtkspell" = yes; then
        AC_MSG_RESULT(yes)
        AC_MSG_CHECKING([whether GtkSpell is available])
        if $PKG_CONFIG gtkspell-2.0 ; then
                AC_MSG_RESULT(yes)
                CFLAGS="$CFLAGS `$PKG_CONFIG --cflags gtkspell-2.0`"
                LIBS="$LIBS `$PKG_CONFIG --libs gtkspell-2.0`"
                AC_DEFINE(USE_GTKSPELL, 1, Use GtkSpell in editor)
        else
                AC_MSG_RESULT(no)
                ac_cv_enable_gtkspell=no
        fi
else
        AC_MSG_RESULT(no)
fi

# i18n
GETTEXT_PACKAGE=geanyvc
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])

ALL_LINGUAS="`sed -e '/^#/d' $srcdir/po/LINGUAS`" # take all languages found in file po/LINGUAS

AM_GLIB_GNU_GETTEXT
# workaround for intltool bug (http://bugzilla.gnome.org/show_bug.cgi?id=490845)
if test "x$MSGFMT" = "xno"; then
	AC_MSG_ERROR([msgfmt not found. Please install the gettext package.])
fi

# intltool hack to define install_sh on Debian/Ubuntu systems
if test "x$install_sh" = "x"; then
	install_sh="`pwd`/install-sh"
	AC_SUBST(install_sh)
fi

# get the plugin installed at the correct location for Geany
# TODO find a way to NOT override --libdir command line option if given
libdir="`$PKG_CONFIG --variable=libdir geany`/geany"

dnl unittests
PKG_CHECK_MODULES([CHECK], [check >= 0.9.4], [
AC_SUBST(CHECK_CFLAGS)
AC_SUBST(CHECK_LIBS)
AM_CONDITIONAL(UNITTESTS, true)
], [
AM_CONDITIONAL(UNITTESTS, false)
])


AC_OUTPUT([
Makefile
po/Makefile.in
tests/Makefile
])

echo "----------------------------------------"
echo "Install geanyvc in                 : ${libdir}"
echo "Using Geany version                : ${GEANY_VERSION}"
echo "Using GTK version                  : ${GTK_VERSION}"
echo "Building with GtkSpell support     : ${ac_cv_enable_gtkspell}"
echo ""
echo "Configuration is done OK."
echo ""
